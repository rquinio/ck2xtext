/**
 * Simple grammar generic to CK2 files
 * 
 * Note: grammar namespace defines the java packages
 */
grammar ck2xtext.Ck2 hidden(WS, SL_COMMENT)

generate ck2 "http://www.crusaderkings.com" 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	(properties+= Property)+
;


terminal fragment DIGIT : ('0'..'9');

terminal BOOL returns ecore::EBoolean: ('yes' | 'no');
terminal INT returns ecore::EInt: (DIGIT)+;

terminal fragment NON_ASCII_CAP : 'À'|'Á'|'Â'|'Ã'|'Ä'|'Å'|'Æ'|'Ç'|'È'|'É'|'Ê'|'Ë'|'Ì'|'Í'|'Î'|'Ï'|'Ð'|'Ñ'|'Ò'|'Ó'|'Ô'|'Õ'|'Ö'|'Ø'|'Š'|'Ù'|'Ú'|'Û'|'Ü'|'Ý'|'Ÿ'|'Þ'|'ß'|'Œ';
terminal fragment NON_ASCII_LOW : 'à'|'á'|'â'|'ã'|'ä'|'å'|'æ'|'ç'|'è'|'é'|'ê'|'ë'|'ì'|'í'|'î'|'ï'|'ð'|'ñ'|'ò'|'ó'|'ô'|'õ'|'ö'|'ø'|'š'|'ù'|'ú'|'û'|'ü'|'ý'|'ÿ'|'ž'|'þ'|'œ';

terminal fragment ID_PART: ('a'..'z'|'A'..'Z'|NON_ASCII_LOW|NON_ASCII_CAP|'<');

/**
 * IDs of entities
 * Used in named definitions (<id> = {}), or as an 'id' property on the entity.
 * Cannot start with a digit, nor special characters _,-, etc.
 * May be namespaced (namespace.id)
 * Note: Caret is used to escape an identifier if there are conflicts with existing keywords. It is removed by the ID rule's ValueConverter. 
 */
terminal ID : ('^')?ID_PART (ID_PART|DIGIT|'_'|'-'|'\''|'’'|':'|'.'|'>')*;

terminal STRING	: '"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|'\\' */ | !('\\'|'"') )* '"';

terminal SL_COMMENT : '#' !('\n'|'\r')* ('\r'? '\n')?;
terminal WS			: (' '|'\t'|'\r'|'\n')+;

Date returns ecore::EDate : INT'.'INT'.'INT;
Double returns ecore::EDouble : ('-'|'+')?INT('.'INT)?;

/**
 * data={0 0 0 3 0 2 2}
 * color = { 0.3 0.55 0.8 }
 */
List:
	'{' (elems+= ListElem)+'}'
;

ListElem returns ecore::EString : ID | STRING | Double;

Property:
	StringProperty |
	IdProperty |
	CommandProperty |
	BoolProperty |
	DateProperty |
	NumericProperty |
	ListProperty |
	//NumericListProperty |
	Clause |
	ProbabilityProperty
;

Clause:
	IdClauseProperty |
	IntClauseProperty |
	DateClauseProperty
;
/**
 * from_dynasty_prefix = "af "
 */
StringProperty:
	key=ID '=' value=STRING
;
/**
 * modifier = default_culture_modifier
 */
IdProperty:
	key=ID '=' value=ID
;
/**
 * portrait = [From.GetID]
 */
CommandProperty:
	key=ID '=' '[' value=ID ']'
;
/**
 * dukes_called_kings = yes
 */
BoolProperty:
	key=ID '=' value=BOOL
;
DateProperty:
	key=ID '=' value=Date
;
NumericProperty:
	key=ID '=' value=Double
;

/**
 * male_names = { Ale Alfr_Alf }
 */
ListProperty:
	key=ID '=' value=List
;
IdClauseProperty:
	name=ID '=' value= '{' (properties+= Property)* '}'
;
IntClauseProperty:
	name=INT '=' value= '{' (properties+= Property)* '}'
;
DateClauseProperty:
	name=Date '=' value= '{' (properties+= Property)* '}'
;

/**
 * 100 = 4000
 * 100 = HL.10610
 */
ProbabilityProperty:
	key=INT '=' value=ListElem
;